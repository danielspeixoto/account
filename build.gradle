group 'account'
version '1.0-SNAPSHOT'


buildscript {
    ext {
        kotlinVersion = "1.2.0-rc-39"
        springBootVersion = '1.5.9.RELEASE'
        apolloGraphqlVersion = '0.4.3'
    }

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "com.apollographql.apollo:apollo-gradle-plugin:${apolloGraphqlVersion }"
    }
}

apply plugin: 'java'
apply plugin: 'com.apollographql.android'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    jcenter()
}

dependencies {
    // Testing
    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    // Reflection
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2")

    // Spring
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    // JWT
    compile 'io.jsonwebtoken:jjwt:0.9.0'

    // GraphQL
    compile group: 'com.graphql-java', name: 'graphql-java', version: '6.0'
    compile group: 'com.graphql-java', name: 'graphql-java-tools', version: '4.3.0'
    compile group: 'com.graphql-java', name: 'graphql-java-servlet', version: '4.7.0'
    compile group: 'com.graphql-java', name: 'graphql-spring-boot-starter', version: '3.10.0'
    compile 'com.graphql-java:graphiql-spring-boot-starter:3.10.0'
    compile "com.apollographql.apollo:apollo-runtime:${apolloGraphqlVersion}"
    // https://mvnrepository.com/artifact/com.squareup.okhttp/okhttp
    compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'

}

def generatedOutputDir = "$buildDir/generated/source"

sourceSets {
    generated {
        java.srcDir "${generatedOutputDir}/apollo"
    }

    test {
        java {
            compileClasspath += generated.output
            runtimeClasspath += generated.output
        }
    }
}

compileGeneratedJava {
    classpath = configurations.compile
}

compileJava {
    dependsOn(compileGeneratedJava)
    source += sourceSets.generated.java
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    source += generatedOutputDir
}
